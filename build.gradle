/*
 * Copyright 2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

plugins {
    id 'com.jfrog.bintray' version '1.8.4' apply false
}

configure(subprojects.findAll { !['spring-distributed-locks-example'].contains(it.name) }) {
    apply plugin: 'groovy'
    apply plugin: 'java-library'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    group 'com.budjb'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        api platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        api 'org.springframework.boot:spring-boot-starter'

        annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
        testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            library(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'budjb'
                            name = 'Bud Byrd'
                            email = 'bud.byrd@gmail.com'
                        }
                    }

                    scm {
                        url = githubHttpsUrl
                        connection = "scm:git:${githubHttpsUrl}.git"
                        developerConnection = "scm:git:${githubGitUrl}"
                    }

                }
            }
        }
    }

    bintray {
        user = System.getenv(bintrayUsernameEnvVar)
        key = System.getenv(bintrayKeyEnvVar)
        publish = true

        pkg {
            websiteUrl = githubHttpsUrl
            issueTrackerUrl = "${githubHttpsUrl}/issues"
            vcsUrl = githubGitUrl
            githubRepo = 'budjb/spring-distributed-locks'

            repo = 'spring'
            name = project.name
            licenses = ['Apache-2.0']
            publications = ['library']

            version {
                name = project.version
                released = new Date()
                vcsTag = project.version
                gpg {
                    sign = true
                    passphrase = System.getenv('GPG_PASSWORD')
                }
            }
        }
    }
}
